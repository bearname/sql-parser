plugins {
	id 'java'
	id 'jacoco'
	id "org.sonarqube" version "3.1.1"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
	mavenCentral()
}

sonarqube {
	properties {
		property "sonar.projectKey", "bearname_buyer-experience"
		property "sonar.organization", "mikushov-mihail-github"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}


jacoco {
	toolVersion = "0.8.6"
}
jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination layout.buildDirectory.dir('jacocoHtml').get().asFile
	}
}

jar {
  archiveBaseName = 'gs-gradle'
  archiveVersion =  '0.1.0'
  manifest {
    attributes "Main-Class": "com.example.restservice.RestServiceApplication"
  }

  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

dependencies {
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
	testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

	implementation group: 'redis.clients', name: 'jedis', version: '3.5.2'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.1.0'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'

}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.1
			}
		}
	}
}